#compdef ifup ifdown

local expl list intf sep
local -a disp

case $OSTYPE in
  aix*)
    intf=( ${(f)"$(lsdev -C -c if -F 'name:description')"} )
    if zstyle -T ":completion:${curcontext}" verbose; then
      zstyle -s ":completion:${curcontext}:" list-separator sep || sep=--
      zformat -a list " $sep " "$intf[@]"
      disp=(-ld list)
    fi
  ;;
  darwin*|freebsd*|dragonfly*) intf=( $(ifconfig -l) ) ;;
  irix*) intf=( ${${${(f)"$(/usr/etc/netstat -i)"}%% *}[2,-1]} ) ;;

  *)
  # Make sure ifconfig is in the path.
  local PATH=$PATH
  PATH=/sbin:$PATH
  intf=( $(ifconfig -a 2>/dev/null | sed -n 's/^\([^ 	:]*\).*/\1/p') )
  if [[ ${#intf} -eq 0 && -d /proc/sys/net/ipv4/conf ]]; then
    # On linux we used to use the following as the default.
    # However, we now use ifconfig since it finds additional devices such
    # as tunnels.  So only do this if that didn't work.
    intf=( /proc/sys/net/ipv4/conf/*~*(all|default)(N:t) )
  fi
  ;;
esac

_wanted interfaces expl 'network interface' \
    compadd "$@" "$disp[@]" - "${(@)intf%%:*}"
