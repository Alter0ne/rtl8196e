Packages: gio-2.0
D-Bus

Program: client

[DBus (name = "org.example.Test")]
interface Test : Object {
	public abstract HashTable<string, HashTable<string, Variant>> test_nested_dict () throws IOError;
}

void main () {
	// client
	Test test = Bus.get_proxy_sync (BusType.SESSION, "org.example.Test", "/org/example/test");

	HashTable<string, HashTable<string, Variant>> dict = test.test_nested_dict ();
	assert (dict.size () == 1);
	HashTable<string, Variant> nested_dict = dict.lookup ("hello");
	assert (nested_dict != null);
	Variant v = nested_dict.lookup ("hello");
	assert (v != null);
	string[] s = (string[]) v;
	assert (s.length == 1 && s[0] == "hello");
}

Program: server

[DBus (name = "org.example.Test")]
class Test : Object {
	public HashTable<string, HashTable<string, Variant>> test_nested_dict () {
		string[] s = { "hello" };
		HashTable<string, Variant> nested_dict = new HashTable<string, Variant> (str_hash, null);
		nested_dict.insert ("hello", s);
		HashTable<string, HashTable<string, Variant>> dict = new HashTable<string, HashTable<string, Variant>> (str_hash, null);
		dict.insert ("hello", nested_dict);
		return dict;
	}
}

MainLoop main_loop;

void client_exit (Pid pid, int status) {
	// client finished, terminate server
	assert (status == 0);
	main_loop.quit ();
}

void main () {
	var conn = Bus.get_sync (BusType.SESSION);
	conn.register_object ("/org/example/test", new Test ());

	// try to register service in session bus
	var request_result = conn.call_sync ("org.freedesktop.DBus", "/org/freedesktop/DBus", "org.freedesktop.DBus", "RequestName",
	                                      new Variant ("(su)", "org.example.Test", 0x4), null, 0, -1);
	assert ((uint) request_result.get_child_value (0) == 1);

	// server ready, spawn client
	Pid client_pid;
	Process.spawn_async (null, { "dbus_bug782719_client" }, null, SpawnFlags.DO_NOT_REAP_CHILD, null, out client_pid);
	ChildWatch.add (client_pid, client_exit);

	main_loop = new MainLoop ();
	main_loop.run ();
}
