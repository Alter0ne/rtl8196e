Packages: gio-2.0
D-Bus

Program: client

[DBus (name = "org.example.Test")]
interface Test : Object {
	public abstract async void test_void () throws Error;
	public abstract async int test_int (int i, out int j) throws Error;
	public abstract async string test_string (string s, out string t) throws Error;
	public abstract async void test_cancellable (Cancellable? cancellable = null) throws Error;
}

MainLoop main_loop;

async void run () {
	Test test = yield Bus.get_proxy (BusType.SESSION, "org.example.Test", "/org/example/test");

	try {
		yield test.test_void ();
		assert_not_reached ();
	} catch {
	}

	try {
		int j, k;
		k = yield test.test_int (42, out j);
		assert_not_reached ();
	} catch {
	}

	try {
		string t, u;
		u = yield test.test_string ("hello", out t);
		assert_not_reached ();
	} catch {
	}

	try {
		var cancellable = new Cancellable ();
		cancellable.cancel ();
		yield test.test_cancellable (cancellable);
		assert_not_reached ();
	} catch {
	}

	main_loop.quit ();
}

void main () {
	// client
	run.begin ();

	main_loop = new MainLoop (null, false);
	main_loop.run ();
}

Program: server

[DBus (name = "org.example.Test")]
class Test : Object {
	public async void test_void () throws Error {
		Idle.add (test_void.callback);
		yield;
		throw new IOError.FAILED ("Operation failed");
	}

	public async int test_int (int i, out int j) throws Error {
		Idle.add (test_int.callback);
		yield;
		throw new IOError.FAILED ("Operation failed");
	}

	public async string test_string (string s, out string t) throws Error {
		Idle.add (test_string.callback);
		yield;
		throw new IOError.FAILED ("Operation failed");
	}

	public async void test_cancellable (Cancellable? cancellable = null) throws Error {
		Idle.add (test_cancellable.callback);
		yield;
	}
}

MainLoop main_loop;

void client_exit (Pid pid, int status) {
	// client finished, terminate server
	assert (status == 0);
	main_loop.quit ();
}

void main () {
	var conn = Bus.get_sync (BusType.SESSION);
	conn.register_object ("/org/example/test", new Test ());

	// try to register service in session bus
	var request_result = conn.call_sync ("org.freedesktop.DBus", "/org/freedesktop/DBus", "org.freedesktop.DBus", "RequestName",
	                                      new Variant ("(su)", "org.example.Test", 0x4), null, 0, -1);
	assert ((uint) request_result.get_child_value (0) == 1);

	// server ready, spawn client
	Pid client_pid;
	Process.spawn_async (null, { "dbus_async_errors_client" }, null, SpawnFlags.DO_NOT_REAP_CHILD, null, out client_pid);
	ChildWatch.add (client_pid, client_exit);

	main_loop = new MainLoop ();
	main_loop.run ();
}
