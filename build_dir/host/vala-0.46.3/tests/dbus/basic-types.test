Packages: gio-2.0
D-Bus

Program: client

[DBus (name = "org.example.Test")]
interface Test : Object {
	public abstract string test_property { owned get; set; }
	public abstract int test_int_property { get; set; }

	public abstract void test_void () throws IOError;
	public abstract int test_int (int i, out int j) throws IOError;
	public abstract string test_string (string s, out string t) throws IOError;
}

void main () {
	// client
	Test test = Bus.get_proxy_sync (BusType.SESSION, "org.example.Test", "/org/example/test", DBusProxyFlags.DO_NOT_LOAD_PROPERTIES);

	test.test_void ();

	int j, k;
	k = test.test_int (42, out j);
	assert (j == 23);
	assert (k == 11);

	string t, u;
	u = test.test_string ("hello", out t);
	assert (t == "world");
	assert (u == "vala");

	test.test_property = "hello";
	t = test.test_property;
	assert (t == "hello");

	test.test_int_property = 42;
	j = test.test_int_property;
	assert (j == 42);
}

Program: server

[DBus (name = "org.example.Test")]
class Test : Object {
	public string test_property { owned get; set; }
	public int test_int_property { get; set; }

	public void test_void () {
	}

	public int test_int (int i, out int j) {
		assert (i == 42);
		j = 23;
		return 11;
	}

	public string test_string (string s, out string t) {
		assert (s == "hello");
		t = "world";
		return "vala";
	}
}

MainLoop main_loop;

void client_exit (Pid pid, int status) {
	// client finished, terminate server
	assert (status == 0);
	main_loop.quit ();
}

void main () {
	var conn = Bus.get_sync (BusType.SESSION);
	conn.register_object ("/org/example/test", new Test ());

	// try to register service in session bus
	var request_result = conn.call_sync ("org.freedesktop.DBus", "/org/freedesktop/DBus", "org.freedesktop.DBus", "RequestName",
	                                      new Variant ("(su)", "org.example.Test", 0x4), null, 0, -1);
	assert ((uint) request_result.get_child_value (0) == 1);

	// server ready, spawn client
	Pid client_pid;
	Process.spawn_async (null, { "dbus_basic_types_client" }, null, SpawnFlags.DO_NOT_REAP_CHILD, null, out client_pid);
	ChildWatch.add (client_pid, client_exit);

	main_loop = new MainLoop ();
	main_loop.run ();
}
